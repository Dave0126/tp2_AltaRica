/*
* Phased mission system:
* Satellite Communication System
*
*/
block System
	// Radar subsystems
	GroundStationSubSystem GS1, GS2;
	SatelliteSubSystem Sat; 
	
	// Relay satellite subsystems
	SatelliteSubSystem SatRelay1, SatRelay2; 

	// Phases modeling
	block PhaseController
		Integer vsPhase (init = 1);
		// parameters
		// ...batteries_fail
		parameter Real D1 = 2.0;
		parameter Real D2 = 1.0;
		parameter Real D3 = 2.0;
		parameter Real D4 = 7.0;

		// events 
		// ...batteries_fail
		event evPhase1_2 (delay = Dirac(D1));
		event evPhase2_3 (delay = Dirac(D2));
		event evPhase3_4 (delay = Dirac(D3));
		event evPhase4_1 (delay = Dirac(D4));


		transition	
		//...
			evPhase1_2: vsPhase == 1 -> vsPhase := 2;
			evPhase2_3: vsPhase == 2 -> vsPhase := 3;
			evPhase3_4: vsPhase == 3 -> vsPhase := 4;
			evPhase4_1: vsPhase == 4 -> vsPhase := 1;
	end

	// Subsystem used during the 1st phase
	block Phase1
		embeds main.GS1 as GS1;
		embeds main.Sat as Sat;
		// ...
		Boolean vfWorking ( reset = false );
		assertion
		//...
			vfWorking := GS1.vfOutput and Sat.vfOutput;
	end

	// Subsystem used during the 2nd phase
	block Phase2
		// ...
		embeds main.GS1 as GS1;
		embeds main.GS2 as GS2;
		embeds main.Sat as Sat;
		Boolean vfWorking ( reset = false );
		assertion
		// ...vsWorking
			vfWorking := (GS1.vfOutput or GS2.vfOutput) and Sat.vfOutput;
	end

	// Subsystem used during the 3rd phase
	block Phase3
		// ...
		embeds main.GS2 as GS2;
		embeds main.Sat as Sat;
		Boolean vfWorking( reset = false );
		assertion
		// ...
			vfWorking := Sat.vfOutput and GS2.vfOutput;
	end

	// Subsystem used during the 4th phase
	block Phase4
		// ...
		embeds main.GS1 as GS1;
		embeds main.GS2 as GS2;
		embeds main.SatRelay1 as SatRelay1;
		embeds main.SatRelay2 as SatRelay2;
		embeds main.Sat as Sat;
		Boolean vfWorking( reset = false );
		assertion
		// ...
			vfWorking := (GS1.vfOutput or GS2.vfOutput) and Sat.vfOutput and (SatRelay1.vfOutput or SatRelay2.vfOutput);
	end

	assertion
		// vfWorking := ...;
		vfWorking := if (PhaseController.vsPhase == 1) then Phase1.vfWorking
					else if (PhaseController.vsPhase == 2) then Phase2.vfWorking
					else if (PhaseController.vsPhase == 3) then Phase3.vfWorking
					else if (PhaseController.vsPhase == 4) then Phase4.vfWorking
					else false;

	// Observer oFailed
	//observer Boolean oFailed = ...;
	observer Boolean oFailed = not vfWorking;

end

